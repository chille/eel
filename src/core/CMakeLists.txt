include(CheckIncludeFiles)
include(CheckFunctionExists)

include_directories(${EEL_BINARY_DIR}/include)
include_directories(${EEL_SOURCE_DIR})
include_directories(${EEL_SOURCE_DIR}/include)
include_directories(${EEL_SOURCE_DIR}/src/core)
include_directories(${EEL_SOURCE_DIR}/src/core/eelc)

set(EEL_DIRSEP	/)

if(WIN32)
	set(EEL_ARCH "WIN32")
	set(EEL_SOEXT ".DLL")
	set(EEL_MODULE_DIR "${CMAKE_INSTALL_PREFIX}/modules")
elseif(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
	set(EEL_ARCH "Linux")
	set(EEL_SOEXT ".so")
	set(EEL_MODULE_DIR "${CMAKE_INSTALL_PREFIX}/lib/eel")
else()
	set(EEL_ARCH "Unknown")
	set(EEL_SOEXT ".so")
	set(EEL_MODULE_DIR "${CMAKE_INSTALL_PREFIX}/lib/eel")
endif()

set(CMAKE_EXTRA_INCLUDE_FILES setjmp.h)
check_function_exists(setjmp		HAVE_SETJMP)
check_function_exists(_setjmp		HAVE__SETJMP)
check_function_exists(longjmp		HAVE_LONGJMP)
check_function_exists(_longjmp		HAVE__LONGJMP)
check_function_exists(sigsetjmp		HAVE_SIGSETJMP)
check_function_exists(siglongjmp	HAVE_SIGLONGJMP)

set(CMAKE_EXTRA_INCLUDE_FILES stdio.h)
check_function_exists(snprintf		HAVE_SNPRINTF)
check_function_exists(_snprintf		HAVE__SNPRINTF)

set(CMAKE_EXTRA_INCLUDE_FILES)


#TODO:
#builtin.c:      builtin.eel text2c.sed
#                sed -f text2c.sed builtin.eel > builtin.c


# Core files
set(sources
	e_array.c
	e_cast.c
	e_state.c
	e_table.c
	e_vector.c
	e_dstring.c
	e_function.c
	e_object.c
	e_builtin.c
	e_class.c
	e_register.c
	e_string.c
	e_util.c
	e_vm.c
	e_error.c
	e_module.c
	e_operate.c
	e_exceptions.c
	e_sharedstate.c
)

# Compiler files
set(sources ${sources}
	eelc/ec_bio.c
	eelc/ec_coder.c
	eelc/ec_context.c
	eelc/ec_event.c
	eelc/ec_lexer.c
	eelc/ec_manip.c
	eelc/ec_mlist.c
	eelc/ec_parser.c
	eelc/ec_symtab.c
	eelc/ec_optimizer.c
)

add_library(libeel ${sources})

if(UNIX)
	target_link_libraries(libeel m)
endif(UNIX)

if(WIN32)
	target_link_libraries(libeel winmm)
endif(WIN32)

# Until 1.0, we assume there's no ABI compatibility across releases!
set_target_properties(libeel PROPERTIES VERSION ${VERSION_STRING}
	SOVERSION ${VERSION_STRING})

set_target_properties(libeel PROPERTIES OUTPUT_NAME "eel")

set(EEL_AUTO_MESSAGE "This file is automatically generated. Do not edit!")

configure_file(${CMAKE_SOURCE_DIR}/config.h.cmake
	${CMAKE_BINARY_DIR}/include/config.h)

configure_file(${CMAKE_SOURCE_DIR}/include/EEL_version.h.cmake
	${CMAKE_BINARY_DIR}/include/EEL_version.h @ONLY)
install(FILES ${CMAKE_BINARY_DIR}/include/EEL_version.h
	DESTINATION include/EEL)

configure_file(${CMAKE_SOURCE_DIR}/include/EEL_platform.h.cmake
	${CMAKE_BINARY_DIR}/include/EEL_platform.h @ONLY)
install(FILES ${CMAKE_BINARY_DIR}/include/EEL_platform.h
	DESTINATION include/EEL)

install(TARGETS libeel DESTINATION lib)
