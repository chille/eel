///////////////////////////////////////////////
// chat2.eel - Graphical Chat Toy, Version 2
// Copyright 2005, 2009, 2017 David Olofson
///////////////////////////////////////////////
//
// This version adds an offscreen buffer, and
// local and remote cursors.
//
// This is actually pretty tricky to do with
// SDL2, since there's no universal, efficient
// way of rendering into an offscreen buffer,
// that is then copied to the window. There
// are render targets (which we rely on here),
// but they're not always supported. There are
// surfaces, but there's no straightforward,
// efficient way of rendering them to a window.
//
///////////////////////////////////////////////

eelversion 0.3.7;

import SDL as SDL;
import NET2 as NET2;
import io, math;

function Pen(renderer, canvas, color)
{
	local o = {
		// Private stuff
		.lastx		0,
		.lasty		0,
		.drawing	false,
		.renderer	renderer,
		.canvas		canvas,

		// Interface
		.color		color,		// Pen color

		// Pen down
		procedure Down(self)
		{
			self.drawing = true;
		}

		// Move or draw
		procedure To(self, x, y)
		{
			local r = self.renderer;
			local dirty = [];

			// Remove cursor
			dirty.+ SDL.Rect [self.(lastx, lasty) - 2, 8, 8];

			// Draw some
			if self.drawing
			{
				SDL.SetRenderTarget(r, self.canvas);
				SDL.SetRenderDrawColor(r, self.color);
				SDL.RenderDrawLine(r,
						self.(lastx, lasty), x, y);
				local ux, local uy = (x, y) |<
						self.(lastx, lasty);
				local uw = abs(x - self.lastx);
				local uh = abs(y - self.lasty);
				dirty.+ Rect [ux, uy, (uw, uh) + 1];
				SDL.RenderPresent(r);
				SDL.SetRenderTarget(r);
			}

			for local i = 0, sizeof dirty - 1
				SDL.RenderCopy(r, self.canvas,
						dirty[i], dirty[i]);

			self.(lastx, lasty) = x, y;

			// Plot cursor
			SDL.SetRenderDrawColor(r, 0xffffff);
			SDL.RenderDrawPoint(r,
					(x - 2, y), (x - 1, y),
					(x, y - 2), (x, y - 1),
					(x + 1, y), (x + 2, y),
					(x, y + 1), (x, y + 2));
			SDL.SetRenderDrawColor(r, self.color);
			SDL.RenderFillRect(r, Rect [(x, y) + 2, 4, 4]);
		}

		// Pen up
		procedure Up(self)
		{
			self.drawing = false;
		}
	};
	return o;
}

export function main<args>
{
	if specified args[1]
		local host = args[1];
	else
		host = "localhost";
	if specified args[2]
		local port = (integer)args[2];
	else
		port = 6666;

	// Try to connect to a server. Whether or not that
	// succeeds, also try to set up a server socket,
	// so we can accept new connections after our peer
	// disconnects.
	local peer, local server = nil;
	try
		peer = NET2.Socket [host, port];
	try
		server = NET2.TCPAcceptOn(port);

	local window = SDL.Window ["Graphical Chat 2",
			SDL.WINDOWPOS_CENTERED, SDL.WINDOWPOS_CENTERED,
			640, 480];
	local renderer = SDL.Renderer [window];

	if not SDL.RenderTargetSupported(renderer)
	{
		print("Render targets not supported!\n");
		print("This example will not work without them.\n");
		return 1;
	}

	local canvas = SDL.Texture [renderer, SDL.PIXELFORMAT_RGB888,
			SDL.TEXTUREACCESS_TARGET, renderer.(w, h)];
	SDL.ShowCursor(false);

	local localpen = Pen(renderer, canvas, 0xffff00);
	local remotepen = Pen(renderer, canvas, 0x00ffff);

	// Clear the canvas texture
	SDL.SetRenderTarget(renderer, canvas);
	SDL.SetRenderDrawColor(renderer, 0x000000);
	SDL.RenderClear(renderer);
	SDL.RenderPresent(renderer);
	SDL.SetRenderTarget(renderer);

	local buf = memfile [];

	procedure handle(data)
	{
		upvalue remotepen;
		upvalue buf.position = sizeof buf;
		write(buf, data);
		buf.position = 0;
		try while true
		decodeloop:
		{
			switch read(buf, 1)[0]
			  case 'D'
				remotepen:Down();
			  case 'U'
				remotepen:Up();
			  case 'T'
			  {
				if sizeof buf < (2 * 4 + 1)
				{
					buf.position -= 1;
					break decodeloop;
				}
				local x = read(buf, integer);
				local y = read(buf, integer);
				remotepen:To(x, y);
			  }
			  default
				throw "Received corrupt data!";
		}
		delete(buf.buffer, 0, buf.position);
	}

	while true
	mainloop:
	{
		local ev = SDL.PollEvent();
		if not ev
		{
			SDL.RenderPresent(renderer);
			ev = SDL.WaitEvent();
		}
		switch ev.type
		  case SDL.MOUSEBUTTONDOWN
		  {
			localpen:Down();
			if peer
				NET2.TCPSend(peer, "D");
		  }
		  case SDL.MOUSEMOTION
		  {
			localpen:To(ev.(x, y));
			if peer
			{
				local bb = memfile [];
				write(bb, "T", (integer)ev.x, (integer)ev.y);
				NET2.TCPSend(peer, bb.buffer);
			}
		  }
		  case SDL.MOUSEBUTTONUP
		  {
			localpen:Up();
			if peer
				NET2.TCPSend(peer, "U");
		  }
		  case NET2.TCPACCEPTEVENT
		  {
			// We only want one client!
			if peer
				NET2.TCPClose(ev.socket);
			else
				peer = ev.socket;
		  }
		  case NET2.TCPRECEIVEEVENT
		  {
			while true
			{
				local d = NET2.TCPRead(ev.socket);
				if d == nil
					break;
				handle(d);
			}
		  }
		  case NET2.TCPCLOSEEVENT
		  {
			if ev.socket == peer
				peer = nil;
			NET2.TCPClose(ev.socket);
			// In case we were talking to a local server,
			// try to become a server when we're left alone.
			if not server
				try
					server = NET2.TCPAcceptOn(port);
		  }
		  case NET2.ERROREVENT
			print("NET2 error: ", ev.error, "\n");
		  case SDL.WINDOWEVENT
		  {
			switch ev.event
			  case SDL.WINDOWEVENT_EXPOSED
				SDL.RenderCopy(renderer, canvas);
		  }
		  case SDL.KEYUP
			if ev.sym == SDL.KESCAPE
				break mainloop;
		  case SDL.QUIT
			break mainloop;
	}
	return 0;
}
